#version 460 core

layout (local_size_x = 32, local_size_y = 1, local_size_z = 1) in;


struct Tile
{
    float xpos;
    float ypos;
    float zpos;
    /* 
        0 = 그냥
        1 = 충돌
        2 = 사라짐
     */
    uint state;
    uint story;
};

// SSBO
layout(std430, binding = 1) buffer TileBuffer
{
    Tile tileData[];
};



struct OutputData
{
    float HasCharCollision;

    // -1 or +1 으로 토글
    float HasWriteColTile;
    float HasInformColTile;
    float HasInformDisTile;
    
    uint WriteColTile;
    uint InformColTile;
    uint InformDisTile;
};

layout(std430, binding = 2) buffer OutputBuffer
{
    OutputData outputdata;
};




uniform uint TileCount;
uniform vec3 TileScale;
uniform vec3 CharScale;

uniform vec3 MainCharPos;
uniform uint CharStory;




void main()
{
    // ssbo 에 접근하는 스레드의 인덱스
    uint Index = gl_GlobalInvocationID.x;

    // 해당 스레드와 대응되는 데이터가 없으면 종료
    if(Index >= TileCount) return;




    // 현재 스레드가 담당하는 타일
    Tile curTile = tileData[Index];

        // 가장 먼저 output 버퍼를 통해서, 갱신해야 하는 정보가 있는 지 확인한다
        // CPU 에서 충돌 타일 인덱스가 업데이트 됐다면
        if(outputdata.HasInformColTile == +1.0f)
        {
            // 자신의 인덱스가 같다면, 타일의 상태를 업데이트 한다
            if(outputdata.InformColTile == Index)
            {
                // 이제 충돌 상태
                curTile.state = 1;

                // 갱신하는 스레드가 output 버퍼를 초기화 시킨다
                outputdata.HasInformColTile = -1.0f;
            }
        }


        // CPU 에서 사라진 타일 인덱스가 업데이트 됐다면
        if(outputdata.HasInformDisTile == +1.0f)
        {
            if(outputdata.InformDisTile == Index)
            {
                // 이제 사라진 상태
                curTile.state = 2;

                outputdata.HasInformDisTile = -1.0f;
            }
        }

        
        // 사라진 타일이라면 더이상 진행하지 않는다
        // 사라진 타일의 상태는 더이상 업데이트 하지 않는다
        if(curTile.state == 2)
        {
            tileData[Index] = curTile;
            return;
        }



                
        // 이미 캐릭터가 해당 타일의 층을 지났다면, 절대 캐릭터와 충돌이 일어나지 않는다
        // 따라서 바로 함수를 종료한다
        if(curTile.story < CharStory)
        {
            tileData[Index] = curTile;
            
            return;
        }



        // 충돌 판정 => 메인 캐릭터가 타일을 밟는 경우에 대해서만 판정하자
        // 일단 무조건 충돌이 발생했다고 생각
        bool result = true;

        // 캐릭터가 밑에 있다면 밟는 상황이 아니다
        if(MainCharPos.y < curTile.ypos) result = false;

        // 캐릭터와 타일 모두 직육면체 모양, 따라서 서로 충돌한다면 x,y,z 모든 방향에서 충돌이 일어날 것이다
        // x, y, z 축 중 하나라도 떨어져 있다면 충돌 상황이 아니다
             if(abs(MainCharPos.x - curTile.xpos) > (CharScale.x * 0.5 + TileScale.x * 0.5)) result = false;
        else if(abs(MainCharPos.y - curTile.ypos) > (CharScale.y * 0.5 + TileScale.y * 0.5)) result = false;
        else if(abs(MainCharPos.z - curTile.zpos) > (CharScale.z * 0.5 + TileScale.z * 0.5)) result = false;



        // 충돌한 경우 => 일단 메인 캐릭터는 멈추게 된다
        if(result == true)
        {
            // 일단 무조건 메인 캐릭터는 멈춘다는 것을 입력
            outputdata.HasCharCollision = +1.0f;

            // 이미 충돌 상태의 타일이라면, 업데이트할 내용이 없다, 바깥에 알리지 않는다
            if(curTile.state == 1)
            {
                tileData[Index] = curTile;
                return;
            }
            // 충돌 상태가 아니라면, 바깥에 자신이 충돌했다고 알린다
            else
            {
                outputdata.HasWriteColTile = +1.0f;
                outputdata.WriteColTile = Index;
                // CPU 에서 충돌 인덱스를 입력받고, 큐에 넣는다면 이후 output buffer 를 통해 inform 할 것이다
            }
        }

    tileData[Index] = curTile;
}